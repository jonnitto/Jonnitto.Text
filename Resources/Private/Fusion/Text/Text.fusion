// Namespaces are defined in the Root.fusion file

prototype(Jonnitto.Text:Text) < prototype(Fusion:Tag) {
	layout = ${q(node).property('layout')}
	hasRow = ${this.layout == 'two' || this.layout == 'three' ? true : false}
	tagName = ${Configuration.setting('Jonnitto.Text.tagName')}

	@context {
		hasRow = ${this.hasRow}
		layout = ${this.layout}
		hasThree = ${this.layout == 'three' ? true : false}
	}

	attributes.class = Fusion:RawArray {
		@process.nodeType >
		type = 'jonnitto-text'
		container = ${Configuration.setting('Jonnitto.Text.container')}
	}

	content = Fusion:Array {
		@process.row = Fusion:Tag {
			@if.hasRow = ${hasRow}
			attributes = Fusion:Attributes {
				class = ${Configuration.setting('Jonnitto.Text.row')}
			}
			content = ${value}
		}
		text = Jonnitto.Text:Editable {
			attributes.class = Fusion:RawArray {
				col = ${hasRow ? Configuration.setting('Jonnitto.Text.col') : false}
			}
		}
		textTwo = Jonnitto.Text:Editable {
			property = 'textTwo'
			@if.hasNeededLayout = ${hasRow}
			attributes.class = Fusion:RawArray {
				col = ${hasRow ? Configuration.setting('Jonnitto.Text.col') : false}
			}
		}
		textThree = Jonnitto.Text:Editable {
			property = 'textThree'
			@if.hasNeededLayout = ${hasThree}
			attributes.class = Fusion:RawArray {
				col = ${hasRow ? Configuration.setting('Jonnitto.Text.col') : false}
			}
		}
	}

	@process.contentElementWrapping {
		expression = Neos.Neos:ContentElementWrapping
		@position = 'end 999999999'
	}

	@exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
